embed-server --admin-only=true

# Turn on trace logging
/subsystem=logging/console-handler=CONSOLE:undefine-attribute(name=level)

# Configure a filesystem-realm with the required user
if (outcome != success) of /subsystem=elytron/filesystem-realm=tck-realm:read-resource
    /subsystem=elytron/filesystem-realm=tck-realm:add(path=tck-users, relative-to=jboss.server.config.dir)
    /subsystem=elytron/filesystem-realm=tck-realm:add-identity(identity=javajoe)
    /subsystem=elytron/filesystem-realm=tck-realm:set-password(identity=javajoe, clear={password="javajoe"})
    /subsystem=elytron/filesystem-realm=tck-realm:add-identity-attribute(identity=javajoe, name=Roles, value=["Manager"])
end-if

# Configure a security domain that makes use of our security realm
if (outcome != success) of /subsystem=elytron/security-domain=tck-security-domain:read-resource
    /subsystem=elytron/security-domain=tck-security-domain:add(realms=[{realm=tck-realm}], default-realm=tck-realm, permission-mapper=default-permission-mapper)
end-if

# Set the EJB security domain
/subsystem=ejb3/application-security-domain=other:write-attribute(name=security-domain, value=tck-security-domain)

# Update Undertow configuration to make use of our security domain
if (outcome != success) of /subsystem=undertow/application-security-domain=tck-security-domain:read-resource
    /subsystem=undertow/application-security-domain=tck-security-domain:add(security-domain=tck-security-domain, enable-jacc=false)
    /subsystem=undertow/application-security-domain=other:write-attribute(name=security-domain, value=tck-security-domain)
end-if

# Set ejb stateless pool max size as 1
/subsystem=ejb3/strict-max-bean-instance-pool=slsb-strict-max-pool:undefine-attribute(name=derive-size)
/subsystem=ejb3/strict-max-bean-instance-pool=slsb-strict-max-pool:write-attribute(name=max-pool-size,value=1)

# The derby module could be added explicitly with a deployment descriptor to the TransactionTests, for now we'll make it
# global.
/subsystem=ee:write-attribute(name=global-modules, value=[{name=org.wildfly.concurrent.tck}])

stop-embedded-server